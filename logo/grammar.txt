# TOKENS

STRING: [a-zA-Z_][a-zA-Z0-9_]*
FLOAT: [+-]?[0-9]*.[0-9]+
INT: [+-]?[0-9]+
TRUE: 'true'
FALSE: 'false'
LBR: '['
RBR: ']'
LPAR: '('
RPAR: ')'
QUOTE: '"'
DOT: '.'
COLON: ':'
PLUS: '+'
MINUS: '-'
TIMES: '*'
DIVIDE: '/'
EQUALS: '='
GTE: '>='
LTE: '<='
GT: '>'
LT: '<'
REPEAT: 'repeat'
FORWARD: 'fd'
BACK: 'bk'
RIGHT: 'rt'
LEFT: 'lt'
PENUP: 'pu'
PENDOWN: 'pd'
PRINT: 'print'
MAKE: 'make'
IF: 'if'



# GRAMMAR

statement_list : 
    statement_list statement
    | statement

statement :
    turtle_instruction
    | repeat_statement
    | if_statement
    | variable_declaration
    | print_statement

turtle_instruction :
    FORWARD expression
    | BACK expression
    | RIGHT expression
    | LEFT expression
    | PENUP
    | PENDOWN

repeat_statement :
    REPEAT expression LBR statement_list RBR

if_statement :
    IF condition LBR statement_list RBR

variable_declaration :
    MAKE word expression

print_statement :
    PRINT word
    | PRINT expression

expression :
    value 
    | name
    | expression PLUS expression
    | expression MINUS expression
    | expression TIMES expression
    | expression DIVIDE expression
    | LPAR expression RPAR 

condition :
    logical_value
    | expression GT expression
    | expression LT expression
    | expression GTE expression
    | expression LTE expression
    | expression EQUALS expression

word : 
    QUOTE STRING

name :
    COLON STRING 

value :
    INT 
    | FLOAT

logical_value :
    TRUE
    | FALSE